plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        buildConfigField 'String', "TEALIUM_FULLSTORY_VERSION", "\"$tealiumFullstoryVersion\""
        buildConfigField 'String', 'TAG', "\"TealiumFullStory-$tealiumFullstoryVersion\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'com.google.android.material:material:1.6.1'

    // Tealium Remote Command
    api 'com.tealium:remotecommands:1.0.1'

    // FullStory
    implementation "com.fullstory:instrumentation-full:$fullstoryVersion@aar"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.8'
    testImplementation "io.mockk:mockk:$mockk_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.release
                groupId 'com.tealium.remotecommands'
                artifactId 'fullstory'
                version tealiumFullstoryVersion
            }
        }

//        repositories {
//            maven {
//                url "s3://maven.tealiumiq.com/android/releases/"
//                credentials(AwsCredentials) {
//                    accessKey AWS_ACCESS_KEY
//                    secretKey AWS_SECRET_KEY
//                    sessionToken AWS_SESSION_TOKEN
//                }
//            }
//        }
    }
}